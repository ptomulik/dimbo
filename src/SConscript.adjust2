# @COPYRIGHT@
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE

Import('env')

from SConsGnu.Cc import CanonCC, CanonCXX, QueryCCTarget, QueryCXXTarget
from distutils import sysconfig

#############################################################################
# CC/CXX
#############################################################################
env.Append(CPPPATH = [ '#%s' % env.Dir('lib').path ])
#############################################################################

#############################################################################
# PROTOC
#############################################################################
#env.Append(PROTOCPROTOPATH = [ env.Dir('lib').srcnode().path ])
#env.Append(PROTOCPROTOPATH = [ env.Dir('lib').path ])
#############################################################################

#############################################################################
# SWIG
#############################################################################
swigflags = [
    '-c++'
]

swigpath = [
    '#%s' % env.Dir('lib').path,
    '#%s' % env.Dir('lib/dimbo/swig').path,
    '/usr/include'
]

# Swig in general
env.Append(SWIGFLAGS = swigflags)
env.Append(SWIGPATH = swigpath)
env.Append(SWIGPATH = [ '/usr/include' ])
#############################################################################


#############################################################################
# SWIGPY
#############################################################################
# SWIGPY_SWIGPATH
env.Append(SWIGPY_SWIGPATH = ['.'])
env.Append(SWIGPY_SWIGPATH = ['#%s' % env.Dir('lib/dimbo/python').path])
env.Append(SWIGPY_SWIGPATH = ['#%s' % env.Dir('lib/dimbo/swig/python').path])
env.Append(SWIGPY_SWIGPATH = swigpath)
#env.Append(SWIGPY_SWIGPATH = ['/usr/include'])

# SWIGPY_SWIGFLAGS
env.Append(SWIGPY_SWIGFLAGS = swigflags)

# SWIGPY_CPPPATH
env.Append(SWIGPY_CPPPATH = [ '#%s' % env.Dir('lib').path ])
env.Append(SWIGPY_CPPPATH = [ '#%s' % env.Dir('lib/dimbo/swig').path ])
env.Append(SWIGPY_CPPPATH = sysconfig.get_python_inc())

# CFLAGS and SWIGPY_CXXFLAGS
env.SetDefault(SWIGPY_CFLAGS = env.get('CFLAGS',[])[:])
env.SetDefault(SWIGPY_CXXFLAGS = env.get('CXXFLAGS',[])[:])

swigver = tuple([int(n) for n in env.subst('$SWIGVERSION').split('.')])

cc = CanonCC(env)
if cc == 'gcc':
    if '-Werror' in env['SWIGPY_CFLAGS']:
        if env.get('CC_HAS_FLAG', {}).get('-Wmaybe-uninitialized'):
            env.AppendUnique(SWIGPY_CFLAGS = [ '-Wno-maybe-uninitialized' ])
        env.AppendUnique(SWIGPY_CFLAGS = [ '-Wno-unused-parameter' ])
        env.AppendUnique(SWIGPY_CFLAGS = [ '-Wno-deprecated-declarations' ])
elif cc  == 'clang':
    if '-Werror' in env['SWIGPY_CFLAGS']:
        if env.get('CC_HAS_FLAG',{}).get('-Wdeprecated-declarations'):
            env.AppendUnique(SWIGPY_CFLAGS = [ '-Wno-deprecated-declarations' ])
        if env.get('CC_HAS_FLAG',{}).get('-Wunused-parameter'):
            env.AppendUnique(SWIGPY_CFLAGS = [ '-Wno-unused-parameter' ])
        if (swigver < (3,)) and env.get('CC_HAS_FLAG',{}).get('-Wdeprecated-register'):
            env.AppendUnique(SWIGPY_CFLAGS = [ '-Wno-deprecated-register' ])

cxx = CanonCXX(env)
if cxx == 'g++':
    if '-Werror' in env['SWIGPY_CXXFLAGS']:
        if env.get('CXX_HAS_FLAG', {}).get('-Wmaybe-uninitialized'):
            env.AppendUnique(SWIGPY_CXXFLAGS = [ '-Wno-maybe-uninitialized' ])
        env.AppendUnique(SWIGPY_CXXFLAGS = [ '-Wno-unused-parameter' ])
        env.AppendUnique(SWIGPY_CXXFLAGS = [ '-Wno-deprecated-declarations' ])
elif cxx == 'clang++':
    if '-Werror' in env['SWIGPY_CXXFLAGS']:
        if env.get('CXX_HAS_FLAG',{}).get('-Wdeprecated-declarations'):
            env.AppendUnique(SWIGPY_CXXFLAGS = [ '-Wno-deprecated-declarations' ])
        if env.get('CXX_HAS_FLAG',{}).get('-Wunused-parameter'):
            env.AppendUnique(SWIGPY_CXXFLAGS = [ '-Wno-unused-parameter' ])
        if (swigver < (3,)) and env.get('CXX_HAS_FLAG',{}).get('-Wdeprecated-register'):
            env.AppendUnique(SWIGPY_CXXFLAGS =  ['-Wno-deprecated-register'])

# SWIGPY_LIBPATH
env.Append(SWIGPY_LIBPATH = [ '$LIBPATH' ])
#############################################################################

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
