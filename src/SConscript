# @COPYRIGHT@
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE

Import('env')

import re
import os
import SCons.Util

env.Tool('cxxtest')
env.Tool('doxyfile')
env.Tool('doxygen')
env.Tool('gcccov')
env.Tool('protoc')
env.Tool('swig')
env.Tool('swigpy')
env.Tool('textfile')

# Code coverage support. The following is necessary to handle properly *.gcno
# and *.gcda files generated by gcc/clang. See README.rst for
# scons-tool-gcccov (https://github.com/ptomulik/scons-tool-gcccov).
env.GCovInjectObjectEmitters()

# Determine compiler and linker flags, additional libraries etc.
# If provided, the default values will be replaced with corresponding values
# from OS environment.
env.Append( CCFLAGS = '-g' )
if re.match(r'^(yes|true|1)$', env.subst('$DIMBO_ENABLE_GCOV'), re.I):
    env.Append( CCFLAGS = '-O0 --coverage' )
    env.Append( LINKFLAGS = '--coverage' )
else:
    env.Append( CCFLAGS = '-O2' )
env.Append( CCFLAGS = '-Werror -Wall -Wextra -pedantic' )
env.Append( CXXFLAGS = '-ansi -std=c++0x' )
env.Append( CFLAGS = '-ansi -std=c1x' )

# Replace default flags with user-specified ones (OS environment).
for var in [ 'CCFLAGS', 'CXXFLAGS', 'CFLAGS', 'LINKFLAGS', 'LIBS',
             'SHCXXFLAGS', 'SHCFLAGS', 'SHLINKFLAGS' ]:
    try:
        env.Replace( **{ var: SCons.Util.CLVar(os.environ[var]) } )
    except KeyError:
        pass

subdirs = [ 'lib', 'app' ]
scripts = [ '%s/SConscript' % s for s in subdirs ]
SConscript( scripts, exports = ['env'] )

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
